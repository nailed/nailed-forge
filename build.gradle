buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "reening"
            url = "http://maven.reening.nl"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'com.google.guava:guava:14.0'
        classpath 'com.google.code.gson:gson:2.2.4'
        classpath 'com.jcraft:jsch:0.1.50'

        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
        classpath 'jk_5.nailed.gradle:nailed-gradle:1.1-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'forge'
apply plugin: 'nailed'
apply plugin: 'maven'
apply plugin: 'idea'

version = '2.0-SNAPSHOT'

nailed {
    minecraftVersion = '1.7.2'
    forgeVersion = '10.12.0.1002'

    versionName = 'Nailed-1.7.2'
    mainClass = 'net.minecraft.launchwrapper.Launch'

    loadingMavenUrl = 'http://maven.reening.nl/'

    launcherDependency = 'jk_5.nailed:launcher:1.0'
    launcherDependency = 'jk_5.nailed.deploy:forge:1.0'
    launcherDependency = 'jk_5.nailed.deploy:minecraft:1.0'
    launcherDependency = 'jk_5.nailed:Nailed-Client:1.0'

    tweakers += 'jk_5.nailed.launcher.UpdatingTweaker'
    tweakers += 'cpw.mods.fml.common.launcher.FMLTweaker'

    remoteProfileDir = 'nailed'

    deployHost = 'maven.reening.nl'
    deployUsername = 'maven'
    deployPassword = project.deploymaven.password
}

minecraft{
    version = '1.7.2-10.12.0.1002'
    assetDir = 'runtime/client/assets'
}

allprojects {
    group = 'jk_5.nailed'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "reening"
            url = "http://maven.reening.nl/"
        }
        maven {
            name 'forge'
            url 'http://files.minecraftforge.net/maven'
        }
        maven {
            name "minecraft"
            url "https://libraries.minecraft.net/"
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    ext.ats = []

    configurations{
        packed
        compile.extendsFrom packed
    }

    jar {
        baseName = 'Nailed-' + baseName
        destinationDir = new File(rootProject.getBuildDir(), 'libs')

        manifest {
            attributes 'version': project.version
            attributes 'mcversion': rootProject.minecraft.version
        }

        from {
            configurations.packed.collect {
                it.isDirectory() ? it : zipTree(it).matching {
                    exclude "META-INF", "META-INF/**", "*META-INF*", "meta-inf"
                    exclude "**.jar", "**/*.jar", "*.jar"
                }
            }
        }
    }

    dependencies {
        compile rootProject
        compile 'org.projectlombok:lombok:1.12.2'

        testCompile 'junit:junit:4.11'
    }

    rootProject.tasks.reobf {
        reobf(tasks.jar) { spec ->
            spec.classpath = sourceSets.main.compileClasspath
        }
    }

    sourceSets.main.resources.getFiles().each { file ->
        if (file.getName().endsWith(".at") || file.getName().endsWith("_at.cfg")){
            ats += file.getName();
            rootProject.minecraft.at file.getPath()
        }
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'
            include 'version.properties'
            expand 'version': project.version, 'mcversion': project.minecraft.version
        }
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
            exclude 'version.properties'
        }
    }
}
