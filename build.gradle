buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "reening"
            url = "http://maven.reening.nl"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'com.google.guava:guava:14.0'
        classpath 'com.google.code.gson:gson:2.2.4'
        classpath 'com.jcraft:jsch:0.1.50'
        classpath 'io.netty:netty-all:4.0.10.Final'
        classpath 'jk_5.asyncirc:ASyncIRCClient:0.1-SNAPSHOT'

        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
        classpath 'jk_5.nailed.gradle:nailed-gradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'nailed'
apply plugin: 'maven'

version = '2.0-SNAPSHOT'

nailed {
    minecraftVersion = '1.7.2'
    forgeVersion = '10.12.0.1024'

    versionName = 'Nailed-1.7.2'
    mainClass = 'net.minecraft.launchwrapper.Launch'

    loadingMavenUrl = 'http://maven.reening.nl/'

    deployedMod = 'Client'
    deployedMod = 'CrashReporter'
    deployed = 'Updater'

    tweaker = 'jk_5.nailed.updater.UpdatingTweaker'
    tweaker = 'cpw.mods.fml.common.launcher.FMLTweaker'

    remoteProfileDir = 'nailed'

    deployHost = 'maven.reening.nl'
    deployUsername = 'maven'
    deployPassword = project.deploymaven.password

    ircServer = 'irc.reening.nl'
    ircChannel = '#nailedupdates'
}

minecraft{
    version = '1.7.2-10.12.0.1024'
    assetDir = 'runtime/client/assets'
}

allprojects {
    group = 'jk_5.nailed'

    repositories {
        mavenLocal()
        maven {
            name = 'reening'
            url = 'http://maven.reening.nl'
        }
        flatDir name: 'localRepository', dirs: '/Users/jeffreykog/Development/Modding/Nailed/nailed-forge/build/dirtyArtifacts'
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    ext.ats = []

    configurations{
        packed
        compile.extendsFrom packed
    }

    jar {
        baseName = 'Nailed-' + baseName
        destinationDir = new File(rootProject.getBuildDir(), 'libs')

        manifest {
            attributes 'version': project.version
            attributes 'mcversion': rootProject.minecraft.version
        }

        from {
            configurations.packed.collect {
                it.isDirectory() ? it : zipTree(it).matching {
                    exclude "META-INF", "META-INF/**", "*META-INF*", "meta-inf"
                    exclude "**.jar", "**/*.jar", "*.jar"
                }
            }
        }
    }

    dependencies {
        compile rootProject
        compile 'org.projectlombok:lombok:1.12.2'

        testCompile 'junit:junit:4.11'
    }

    rootProject.tasks.reobf {
        reobf(tasks.jar) { spec ->
            spec.classpath = sourceSets.main.compileClasspath
        }
    }

    sourceSets.main.resources.getFiles().each { file ->
        if (file.getName().endsWith(".at") || file.getName().endsWith("_at.cfg")){
            ats += file.getName();
            rootProject.minecraft.at file.getPath()
        }
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'
            include 'version.properties'
            expand 'version': project.version, 'mcversion': project.minecraft.version
        }
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
            exclude 'version.properties'
        }
    }
}
