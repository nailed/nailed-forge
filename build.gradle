buildscript {
    repositories {
        //mavenLocal()
        mavenCentral()
        maven {
            name = "reening"
            url = "http://maven.reening.nl"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'jk_5.nailed.gradle:nailed-gradle:1.1.10'
    }
}

if(!project.hasProperty("keystoreLocation")) ext.keystoreLocation = "."
if(!project.hasProperty("keystoreAlias")) ext.keystoreAlias = ""
if(!project.hasProperty("keystorePassword")) ext.keystorePassword = ""

apply plugin: 'nailed'
apply plugin: 'forge'

version = '2.0-SNAPSHOT'

nailed {
    minecraftVersion = '1.7.2'
    forgeVersion = '10.12.0.1052'

    versionName = 'Nailed-1.7.2'
    mainClass = 'net.minecraft.launchwrapper.Launch'

    loadingMavenUrl = 'http://maven.reening.nl/'

    deploy project: 'Client', mod: true
    deploy project: 'Common', mod: true
    deploy project: 'CrashReporter', load: true
    deploy project: 'Updater'

    launcherTweaker = 'jk_5.nailed.updater.UpdatingTweaker'
    tweaker = 'jk_5.nailed.crashreporter.CrashReporterTweaker'
    tweaker = 'cpw.mods.fml.common.launcher.FMLTweaker'

    remoteProfileDir = 'nailed'

    deployHost = 'maven.reening.nl'
    deployUsername = 'maven'

    if(project.hasProperty("deploymaven")){
        deployPassword = project.deploymaven.password
    }
}

createLauncherProfile {
    addDependency "net.minecraftforge:forge:{MC_VERSION}-{FORGE_VERSION}"
    addDependency "net.minecraft:minecraft:{MC_VERSION}"
    addDependency "jk_5.nailed:Nailed-Updater:0.1", "http://maven.reening.nl/"
    //addDependency "jk_5.nailed:Nailed-CrashReporter:1.2", "http://maven.reening.nl/" //TODO: Remove this. let it be class-loaded
}

minecraft {
    version = '1.7.2-10.12.0.1052'
    assetDir = 'runtime/client/assets'
}

allprojects {
    group = 'jk_5.nailed'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    configurations{
        packed
        compile.extendsFrom packed
    }

    jar {
        baseName = 'Nailed-' + baseName
        destinationDir = new File(rootProject.getBuildDir(), 'libs')

        manifest {
            attributes 'Specification-Title': 'Nailed PVP Mod for Minecraft'
            attributes 'Specification-Version': project.version
            attributes 'Specification-Vendor': 'jk-5'
            attributes 'Implementation-Title': 'jk_5.nailed'
            attributes 'Implementation-Version': project.version
            attributes 'Implementation-Vendor': 'jk-5'
        }

        from {
            configurations.packed.collect {
                it.isDirectory() ? it : zipTree(it).matching {
                    exclude "META-INF", "META-INF/**", "*META-INF*", "meta-inf"
                    exclude "**.jar", "**/*.jar", "*.jar"
                }
            }
        }
    }

    if(rootProject.keystoreLocation != "."){
        rootProject.tasks.reobf.doLast {
            project.getLogger().lifecycle(":" + project.name + ":sign")
            ant.signjar(
                    destDir: jar.destinationDir,
                    jar: jar.getArchivePath(),
                    keystore: rootProject.keystoreLocation,
                    alias: rootProject.keystoreAlias,
                    storepass: rootProject.keystorePassword
            )
        }
    }

    dependencies {
        compile 'org.projectlombok:lombok:1.12.2'

        testCompile 'junit:junit:4.11'
    }

    sourceSets.main.resources.getFiles().each { file ->
        if (file.getName().endsWith("_at.cfg")){
            rootProject.minecraft.at file.getPath()
        }
    }

    processResources {
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'
            include 'version.properties'
            expand 'version': project.version, 'mcversion': rootProject.minecraft.version
        }
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
            exclude 'version.properties'
        }
    }
}
